plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.kokimstocktrading'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

// 메인 클래스 지정
springBoot {
    mainClass.set('com.kokimstocktrading.TransactionServiceApplication')
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // common 모듈 의존성 추가 - EnvConfig 포함
    implementation project(':common')

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // PostgreSQL JDBC driver
    implementation 'org.postgresql:postgresql:42.7.3'

    // DSQL 관련 의존성
    implementation 'software.amazon.awssdk:dsql:2.31.32'
    implementation 'software.amazon.dsql:aurora-dsql-hibernate-dialect:1.0.0'

    // Connection pool
    implementation 'com.zaxxer:HikariCP:5.1.0'

    // Swagger (SpringDoc OpenAPI)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    // WebSocket & JSON
    implementation 'org.java-websocket:Java-WebSocket:1.5.7'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'org.slf4j:slf4j-api:2.0.16'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
}

// Spring Boot 내장 Docker 지원
springBoot {
    mainClass.set('com.kokimstocktrading.TransactionServiceApplication')
    buildInfo()
}

// Docker 이미지 빌드 설정
tasks.named('bootBuildImage') {
    imageName = "stock-trading-server:${version}"
    tags = ["stock-trading-server:latest"]
    
    // 환경 변수 설정 - 646MB 가용 메모리에 맞춤
    environment = [
        'BP_JVM_VERSION': '24',
        // 646MB에서 작동하도록 기존 설정 최소 조정
        'JAVA_TOOL_OPTIONS': '-Xss512k -Xmx300M -XX:ReservedCodeCacheSize=180M -XX:MaxMetaspaceSize=120M -XX:MaxDirectMemorySize=8M -XX:+UseSerialGC'
    ]
    
    // 빌드팩 설정
    builder = 'paketobuildpacks/builder-jammy-base:latest'
    
    // 빌드 시간 최적화
    createdDate = 'now'
}

tasks.named('compileJava') {
    options.compilerArgs += ['-Xlint:deprecation', '-Xlint:unchecked']
}

tasks.named('test') {
    useJUnitPlatform()
}
