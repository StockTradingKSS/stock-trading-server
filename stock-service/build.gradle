plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.kokimstocktrading'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

// 메인 클래스 지정
springBoot {
    mainClass.set('com.kokimstocktrading.StockServiceApplication')
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // common 모듈 의존성 추가 - EnvConfig 포함
    implementation project(':common')

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // PostgreSQL JDBC driver
    implementation 'org.postgresql:postgresql:42.7.3'

    // DSQL 관련 의존성
    implementation 'software.amazon.awssdk:dsql:2.31.32'
    implementation 'software.amazon.dsql:aurora-dsql-hibernate-dialect:1.0.0'

    // Connection pool
    implementation 'com.zaxxer:HikariCP:5.1.0'

    // Swagger (SpringDoc OpenAPI)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    // WebSocket & JSON
    implementation 'org.java-websocket:Java-WebSocket:1.5.7'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'org.slf4j:slf4j-api:2.0.16'

    // HTML Parsing
    implementation 'org.jsoup:jsoup:1.18.1'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // Spring Security Crypto (for BCrypt)
    implementation 'org.springframework.security:spring-security-crypto:6.2.1'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.awaitility:awaitility:4.2.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // H2 Database for testing
    testRuntimeOnly 'com.h2database:h2'

    // Mockito agent for Java 21+
    testRuntimeOnly 'org.mockito:mockito-subclass'
}

// Spring Boot 내장 Docker 지원
springBoot {
    mainClass.set('com.kokimstocktrading.StockServiceApplication')
    buildInfo()
}

tasks.named('compileJava') {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['-Xlint:deprecation', '-Xlint:unchecked']
}

tasks.named('test') {
    useJUnitPlatform()
    jvmArgs('-XX:+EnableDynamicAgentLoading')

    // Mockito agent 설정
    systemProperty 'mockito.mock.maker', 'subclass'
}
